plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "1.0.0-rc-1"
    id "signing"
}

group 'com.inqbarna'
version '1.1-SNAPSHOT'

repositories {
    mavenCentral()
    google()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn"]
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"
    compileOnly "com.android.tools.build:gradle-api:7.1.1"
    implementation platform("software.amazon.awssdk:bom:2.17.152")
    implementation "software.amazon.awssdk:secretsmanager"
    implementation "com.google.guava:guava:31.1-jre"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

signing {
    useGpgCmd()
    sign configurations.archives
}

pluginBundle {
    website = "https://github.com/InQBarna/aws_secret_signing_plugin"
    vcsUrl = "https://github.com/InQBarna/aws_secret_signing_plugin"
    tags = ["android", "signing", "secret", "security", "aws"]
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'com.inqbarna.secretsigning'
            implementationClass = 'com.inqbarna.secretsigning.SecretSigningPlugin'
            displayName = "AWS Secret Signing Plugin"
            description = "Get passwords from AWS Secret Manager to configure android signing release configuration"
        }
    }
}

test {
    useJUnitPlatform()
}